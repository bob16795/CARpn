import "std/mem.car"

macro ArrayList T {
  u32 capacity
  u32 len
  T$* data

  def proc init : Self {
    Self result

    result.len 0 =
    result.capacity 0 =
    result.data null =

    result@
  }

  def proc extend Self* : void {
    => self

    self.capacity self.capacity@ 2 * =

    self.capacity@ 0 == if {
      self.capacity 1 =
    }

    self.data self.data@ self.capacity@ T.SIZE * realloc =
  }

  def proc get Self* i32 : T {
    => i
    => self

    self.data@ i32' i T.SIZE * i32' + T*'@
  }

  def proc append Self* T : void {
    => appends
    => self

    self.capacity@ self.len@ 1 + < if {
      self Self.extend
    }

    self.data@ i32' self.len@ T.SIZE * i32' + T*' appends =

    self.len self.len@ 1 + =
  }
}
